plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.ninjaone'
version = '1.0.0'

java {
	sourceCompatibility = '21'
}

bootJar {
	archiveFileName = 'dundie-awards.jar'
}

sourceSets {
	integrationTest {
		java {
			srcDir file('src/integration-test/java')
		}
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += output + compileClasspath
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	include '**/*IT.class'
	shouldRunAfter test
}

tasks.named('compileIntegrationTestJava') {
	dependsOn tasks.named('compileJava')
}

tasks.named('check') {
	dependsOn tasks.named('integrationTest')
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.postgresql:postgresql:42.7.5'
	implementation 'org.flywaydb:flyway-core:11.8.2'
	runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.8.2'

	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
	implementation 'ch.qos.logback:logback-core:1.5.18'
	implementation 'ch.qos.logback:logback-classic:1.5.18'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('integrationTest') {
	useJUnitPlatform()
}
